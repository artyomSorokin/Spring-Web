buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-clover-plugin:2.2.1'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.7.RELEASE")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'com.bmuschko.clover'

springBoot {
    mainClassName = 'com.epam.cdp.Application'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


//-----------------------------BASE CONFIGURATION--------------------------------------------
clover {

    testIncludes = ['**/*Test.java']
    testExcludes = ['**/Mock*.java']

    targetPercentage = '75%'

    report {
        html = true
        pdf = true

        columns {
            coveredMethods format: 'longbar', min: '75'
            coveredStatements format: '%'
            coveredBranches format: 'raw'
            totalPercentageCovered format: '%', scope: 'package'
        }

    }
}

checkstyle {
    checkstyleTest.enabled = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

check.dependsOn cloverGenerateReport


findbugs {
    ignoreFailures=true
    findbugsTest.enabled=false
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = true
    pmdTest.enabled=false
    ruleSets = [
            'java-basic',
            'java-braces',
            'java-clone',
            'java-codesize',
            'java-comments',
            'java-controversial',
            'java-coupling',
            'java-design',
            'java-empty',
            'java-finalizers',
            'java-imports',
            'java-optimizations',
            'java-strictexception',
            'java-strings',
            'java-typeresolution',
            'java-unnecessary',
            'java-unusedcode'
    ]
}

tasks.withType(Pmd) {
    reports{
        xml.enabled=true
        html.enabled=true
    }
}
//----------------------------------------------------------------------------------------------

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "net.logstash.logback:logstash-logback-encoder:5.0"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "com.itextpdf:itextpdf:5.5.13"
    compileOnly "org.projectlombok:lombok"
    

    clover 'org.openclover:clover:4.2.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testCompileOnly "org.projectlombok:lombok"
}
